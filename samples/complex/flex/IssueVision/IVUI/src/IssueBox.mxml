<?xml version="1.0" encoding="utf-8"?>
<mx:Canvas xmlns:mx="http://www.adobe.com/2006/mxml" width="200" height="45" borderStyle="solid"
	click="ItemClick()" initialize="OnInit()" horizontalScrollPolicy="off" verticalScrollPolicy="off">
	
	<mx:Metadata>
		[Event(name=dd.events.SelectionEvent.AfterSelect, type="dd.events.SelectionEvent")]
	</mx:Metadata>
	
	<mx:Script>
		<![CDATA[
			import mx.core.EdgeMetrics;
					
			import dd.events.SelectionEvent;
					
			private static const ColorWhite:uint = 0xffffff;
			private static const ColorBlack:uint = 0;
			private static const ColorSelected:uint = 0x5a80ed;
			private static const PropColor:String = "color";
			private static const PropBackgroundColor:String = "backgroundColor";
			
			private var _selected:Boolean = false;
			private var _issue:Object = "aaa";
			
			public function get Issue():Object
			{
				return _issue;
			}
			
			public function set Issue(value:Object):void
			{
				_issue = value;
			}
			
			private var _title:String = "Issue Title";
			private var _desc:String = "Description";
			private var _date:String = "29.12.2003";
			private var _isOpen:Boolean = true;
			
			public function set IssueTitle(value:String):void
			{
				_title = value;
				Update();
			}
			
			public function set IssueDescription(value:String):void
			{
				_desc = value;
				Update();
			}
			
			public function set IssueDate(value:String):void
			{
				_date = value;
				Update();
			}
			
			public function Setup(title:String, 
								  desc:String,
								  date:String,
								  isOpen:Boolean):void
			{
				_title = title;
				_desc = desc;
				_date = date;
				_isOpen = isOpen;
				Update();
			}
			
			private function Update():void
			{
				if (LabelTitle != null)
					LabelTitle.text = _title;
				if (LabelDescription != null)
					LabelDescription.text = _desc;
				if (LabelStatus != null)
					LabelStatus.text = _isOpen ? "Open" : "Close";
				if (LabelDate != null)
					LabelDate.text = _date;
			}
			
			public function Toogle():void
			{
				if (_selected)
					Unselect();
				else
					Select();
			}
			
			public function Select():void
			{
				if (_selected) return; 
			 	LabelTitle.setStyle(PropColor, ColorWhite);
			 	LabelDescription.setStyle(PropColor, ColorWhite);
			 	LabelStatus.setStyle(PropColor, ColorWhite);
			 	LabelDate.setStyle(PropColor, ColorWhite);
			 	setStyle(PropBackgroundColor, ColorSelected);
			 	_selected = true;
			 	dispatchEvent(new SelectionEvent(this));
			}
			
			public function Unselect():void
			{
				if (!_selected) return; 
			 	LabelTitle.setStyle(PropColor, ColorBlack);
			 	LabelDescription.setStyle(PropColor, ColorBlack);
			 	LabelStatus.setStyle(PropColor, ColorBlack);
			 	LabelDate.setStyle(PropColor, ColorBlack);
			 	setStyle(PropBackgroundColor, ColorWhite);
			 	_selected = false;
			 	invalidateDisplayList();
			}
			
			private function ItemClick():void
			{
				if (!_selected)
					Select();
			}
			
			private function OnInit():void
			{
				Update();
				LabelTitle.truncateToFit = true;
				LabelDescription.truncateToFit = true;
			}
			
			protected override function updateDisplayList(unscaledWidth:Number, unscaledHeight:Number):void
			{
				super.updateDisplayList(unscaledWidth, unscaledHeight);
				
				LabelTitle.width = width - 95;
				LabelDescription.width = width - 70; 
			}
		]]>
	</mx:Script>
	
	<mx:Label x="5" y="5" text="Issue Title" id="LabelTitle"
		click="ItemClick()" width="108"/>
	<mx:Label x="5" y="23" text="Description" id="LabelDescription"
		click="ItemClick()" width="133"/>
	<mx:Label y="23" text="Open" id="LabelStatus" right="20" click="ItemClick()"/>
	<mx:Label y="5" text="29.12.2003" id="LabelDate" right="20" click="ItemClick()"/>
	
</mx:Canvas>
